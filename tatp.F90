PROGRAM TATP
  ! Copyright (c) 2019 Vedran NovakoviÄ‡ <venovako@venovako.eu>
  USE ATF
  USE BIO
  IMPLICIT NONE

  INTEGER, PARAMETER :: FNL = 253

  CHARACTER(LEN=FNL) :: FN
  INTEGER :: N, U, INFO
  DOUBLE PRECISION :: TOL

  DOUBLE PRECISION, ALLOCATABLE :: A(:,:), D(:), V(:)

  CALL READCL(FN, N, TOL, INFO)
  IF (INFO .NE. 0) STOP 'READCL'
  IF (LEN_TRIM(FN) .LE. 0) STOP 'FN'
  IF (N .LE. 0) STOP 'N'
  IF (TOL .LT. ZERO) STOP 'TOL'

  ALLOCATE(A(N,N))
  ALLOCATE(D(N))
  ALLOCATE(V(N-1))

  U = -1
  CALL BIO_OPEN(U, TRIM(FN)//'.S', 'RO', INFO)
  IF (INFO .NE. 0) STOP 'BIO_OPEN(S)'
  CALL BIO_READ_D2(U, N, N, A, INFO)
  IF (INFO .NE. 0) STOP 'BIO_READ_D2(S)'
  CALL BIO_CLOSE(U, INFO)
  IF (INFO .NE. 0) STOP 'BIO_CLOSE(S)'

#ifndef NDEBUG
  CALL WRITE_DMTX(OUTPUT_UNIT, 0, N, N, A, N)
#endif
  CALL DATP(N, A, N, TOL, D, V, INFO)
#ifdef NDEBUG
  IF (INFO .GE. 0) THEN
     U = OUTPUT_UNIT
  ELSE ! error
     U = ERROR_UNIT
  END IF
  WRITE (U,*) 'INFO=', INFO
  FLUSH(U)
  U = -1
#else
  CALL WRITE_DMTX(OUTPUT_UNIT, INFO, N, N, A, N)
#endif

  CALL BIO_OPEN(U, TRIM(FN)//'.F', 'WO', INFO)
  IF (INFO .NE. 0) STOP 'BIO_OPEN(F)'
  CALL BIO_WRITE_D2(U, N, N, A, INFO)
  IF (INFO .NE. 0) STOP 'BIO_WRITE_D2(F)'
  CALL BIO_CLOSE(U, INFO)
  IF (INFO .NE. 0) STOP 'BIO_CLOSE(F)'

  IF (ALLOCATED(V)) DEALLOCATE(V)
  IF (ALLOCATED(D)) DEALLOCATE(D)
  IF (ALLOCATED(A)) DEALLOCATE(A)

CONTAINS

  SUBROUTINE READCL(FN, N, TOL, INFO)
    IMPLICIT NONE
    CHARACTER(LEN=*), INTENT(OUT) :: FN
    INTEGER, INTENT(OUT) :: N, INFO
    DOUBLE PRECISION, INTENT(OUT) :: TOL

    INFO = COMMAND_ARGUMENT_COUNT() - 3
    IF (INFO .NE. 0) THEN
       WRITE (ERROR_UNIT,*) 'tatp.exe FN N TOL'
       FLUSH(ERROR_UNIT)
       RETURN
    END IF

    FN = ''
    CALL GET_COMMAND_ARGUMENT(2, FN, STATUS=INFO)
    IF (INFO .NE. 0) RETURN
    READ (FN,*) N

    FN = ''
    CALL GET_COMMAND_ARGUMENT(3, FN, STATUS=INFO)
    IF (INFO .NE. 0) RETURN
    READ (FN,*) TOL

    FN = ''
    CALL GET_COMMAND_ARGUMENT(1, FN, STATUS=INFO)
    IF (INFO .NE. 0) RETURN
  END SUBROUTINE READCL

END PROGRAM TATP
